import com.android.build.api.variant.BuildConfigField
import java.util.regex.*

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
    }
}

apply plugin: 'com.android.library'

android {
    namespace 'com.selligent'
    compileSdk 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
    }

    buildFeatures {
        buildConfig true
    }
}

androidComponents {
    onVariants(selector(), { variant ->
        def selligentSettings = loadSelligentSettings(variant)

        if (selligentSettings != null) {
            try {
                variant.buildConfigFields.put("SELLIGENT_SETTINGS", new BuildConfigField("String", "\"" + selligentSettings + "\"", "Selligent settings"))
            }
            catch (Exception ignored) {
                throw new Exception("Selligent requires Android Gradle plugin version 7.0.0 or higher.")
            }
        }
    })
}

repositories {
    mavenCentral()
    maven {
        url "$projectDir/../node_modules/react-native/android"
    }
    google()
    maven { url 'https://developer.huawei.com/repo/' }
}

dependencies {
    api ('com.facebook.react:react-native:+') {
        exclude group:'com.facebook.fbjni'
        exclude group:'com.facebook.yoga'
    }
    implementation 'com.selligent.sdk:selligent_mobile_sdk:4.4.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.firebase:firebase-messaging:23.2.1'
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
}

def loadSelligentSettings(variant) {
    def currentFlavor = getCurrentFlavor()
    def defaultSelligentFileName = "selligent.json"
    File defaultFile = new File("$project.rootDir/../$defaultSelligentFileName")

    if (currentFlavor != "") {
        defaultFile = new File("$project.rootDir/../$currentFlavor/$defaultSelligentFileName")
    }

    def variantSelligentFileName = "selligent.${variant.getName()}.json"
    File variantFile = new File("$project.rootDir/../$variantSelligentFileName")

    def resultingSettings = null

    if (variantFile.exists()) {
        println("Reading Selligent properties for variant ${variant.getName()} using $variantSelligentFileName")
        resultingSettings = ""

        variantFile.eachLine { String line ->
            resultingSettings += line.replace("\"", "\\\"").replaceAll("\\s", "")
        }
    }
    else if (defaultFile.exists()) {
        println("Reading Selligent properties from default ${defaultFile.name} (no variant specific $variantSelligentFileName was found)")
        resultingSettings = ""

        defaultFile.eachLine { String line ->
            resultingSettings += line.replace("\"", "\\\"").replaceAll("\\s", "")
        }
    }
    else {
        throw new InvalidUserDataException("The selligent.json file could not be found. Please make sure you provide this file in the root of this project.")
    }

    return resultingSettings
}

def getCurrentFlavor() {
    Gradle gradle = getGradle()
    String tskReqStr = gradle.getStartParameter().getTaskRequests().toString()
    Pattern pattern = Pattern.compile("(install|assemble)(\\w+)(Release|Debug)")
    Matcher matcher = pattern.matcher(tskReqStr)

    if (matcher.find()) {
        return matcher.group(2).toLowerCase()
    }
    else {
        return ""
    }
}