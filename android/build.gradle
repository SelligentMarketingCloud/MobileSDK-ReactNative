buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        ndkVersion = "21.4.7075529"
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }
}

androidComponents {
    onVariants(selector(), { variant ->
        def selligentSettings = loadSelligentSettings(variant)
        if (selligentSettings != null) {
            try {
                variant.addBuildConfigField("SELLIGENT_SETTINGS", "\"" + selligentSettings + "\"", "Selligent settings")
            } catch (MissingMethodException ignored) {
                throw new Exception("Selligent requires Android Gradle plugin version 4.2.0 or higher.")
            }
        }
    })
}

repositories {
    maven {
        url "$projectDir/../node_modules/react-native/android"
    }
    mavenCentral()
}

dependencies {
    implementation(name: 'sdk-release', ext: 'aar')

    implementation 'com.facebook.react:react-native:+'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.plotprojects:plot-android:3.13.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.firebase:firebase-messaging:21.0.0'
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
}

def configureReactNativePom(def pom) {
    def packageJson = new groovy.json.JsonSlurper().parseText(file('../package.json').text)

    pom.project {
        name packageJson.title
        artifactId packageJson.name
        version = packageJson.version
        group = "com.selligent"
        description packageJson.description
        url packageJson.repository.baseUrl

        licenses {
            license {
                name packageJson.license
                url packageJson.repository.baseUrl + '/blob/master/' + packageJson.licenseFilename
                distribution 'repo'
            }
        }
    }
}

afterEvaluate { project ->

    task androidJavadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += files(android.bootClasspath)
        classpath += files(project.getConfigurations().getByName('compile').asList())
        include '**/*.java'
    }

    task androidJavadocJar(type: Jar, dependsOn: androidJavadoc) {
        archiveClassifier = 'javadoc'
        from androidJavadoc.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
        include '**/*.java'
    }

    android.libraryVariants.all { variant ->
        def name = variant.name.capitalize()
        task "jar${name}"(type: Jar, dependsOn: variant.javaCompileProvider.get()) {
            from variant.javaCompileProvider.get().destinationDir
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifact androidSourcesJar
                artifact androidJavadocJar
            }
        }
    }
}

def loadSelligentSettings(variant) {
    println(variant.flavorName)

    def defaultSelligentFileName = "selligent.json"
    File defaultFile = new File("$project.rootDir/../$defaultSelligentFileName")

    def variantSelligentFileName = "selligent.${variant.getName()}.json"
    File variantFile = new File("$project.rootDir/../$variantSelligentFileName");

    def resultingSettings = null
    if (variantFile.exists()) {
        println("Reading Selligent properties for variant ${variant.getName()} using $variantSelligentFileName")
        resultingSettings = ""
        variantFile.eachLine { String line ->
            resultingSettings += line.replace("\"", "\\\"").replaceAll("\\s", "")
        }
    } else if (defaultFile.exists()) {
        println("Reading Selligent properties from default $defaultSelligentFileName (no variant specific $variantSelligentFileName was found)")
        resultingSettings = ""
        defaultFile.eachLine { String line ->
            resultingSettings += line.replace("\"", "\\\"").replaceAll("\\s", "")
        }
    } else {
        throw new InvalidUserDataException("The selligent.json file could not be found. Please make sure you provide this file in the root of this project.")
    }

    return resultingSettings
}